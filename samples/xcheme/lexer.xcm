/**
 * A XCHEME Lang lexer.
 */
alias token T_EXTRA as '_';
alias token T_ALPHA as from 'A' to 'Z' | from 'a' to 'z';
alias token T_DIGIT as from '0' to '9';
alias token T_WORD  as T_EXTRA | T_ALPHA | T_DIGIT;
alias token T_END   as not T_WORD;

export token <auto> T_KEYWORDS as map {
  <103> ANY     as 'any',
  <104> FROM    as 'from',
  <105> TO      as 'to',
  <106> MAP     as 'map',
  <107> EOS     as 'eos',
  <108> THEN    as 'then',
  <109> ELSE    as 'else',
  <110> OR      as 'or',
  <111> AND     as 'and',
  <112> NOT     as 'not',
  <113> OPT     as 'opt',
  <114> REPEAT  as 'repeat',
  <115> PLACE   as 'place',
  <116> APPEND  as 'append',
  <117> PREPEND as 'prepend',
  <118> PIVOT   as 'pivot',
  <119> NEXT    as 'next',
  <120> LEFT    as 'left',
  <121> RIGHT   as 'right',
  <122> SYMBOL  as 'symbol',
  <123> SCOPE   as 'scope',
  <124> USE     as 'use',
  <125> ERROR   as 'error',
  <126> WARN    as 'warn',
  <127> HAS     as 'has',
  <128> SET     as 'set',
  <129> UNCASE  as 'uncase',
  <130> PEEK    as 'peek',
  <131> SKIP    as 'skip',
  <132> TOKEN   as 'token',
  <133> NODE    as 'node',
  <134> ALIAS   as 'alias',
  <135> AUTO    as 'auto',
  <136> AS      as 'as',
  <137> IMPORT  as 'import',
  <138> EXPORT  as 'export'
} & T_END;

export token <auto> T_SYMBOLS as map {
  <139> ASTERISK      as '*',
  <140> VERTICAL_BAR  as '|',
  <141> AMPERSAND     as '&',
  <142> PERIOD        as '.',
  <143> COMMA         as ',',
  <144> SEMICOLON     as ';',
  <145> OPEN_BRACES   as '{',
  <146> CLOSE_BRACES  as '}',
  <147> GROUP_OPEN    as '(',
  <148> GROUP_CLOSE   as ')',
  <149> CHEVRON_OPEN  as '<',
  <150> CHEVRON_CLOSE as '>'
};

export token <100> T_IDENTIFIER as (T_ALPHA | T_EXTRA) & opt repeat T_WORD;
export token <101> T_NUMBER     as '0' | from '1' to '9' & opt repeat T_DIGIT;
export token <102> T_STRING     as '\'' & repeat ('\\' then * else not '\'' then *) & '\'';