/**
 * A XCHEME Lang lexer.
 */
alias token T_EXTRA as '_';
alias token T_ALPHA as from 'A' to 'Z' | from 'a' to 'z';
alias token T_DIGIT as from '0' to '9';
alias token T_WORD  as T_EXTRA | T_ALPHA | T_DIGIT;
alias token T_END   as not T_WORD;

export token <auto> T_KEYWORDS as map {
  <103> ANY     as 'any',
  <104> FROM    as 'from',
  <105> TO      as 'to',
  <106> MAP     as 'map',
  <107> EOS     as 'eos',
  <108> THEN    as 'then',
  <109> ELSE    as 'else',
  <110> OR      as 'or',
  <111> AND     as 'and',
  <112> NOT     as 'not',
  <113> OPT     as 'opt',
  <114> REPEAT  as 'repeat',
  <115> PLACE   as 'place',
  <116> APPEND  as 'append',
  <117> PREPEND as 'prepend',
  <118> PIVOT   as 'pivot',
  <119> NEXT    as 'next',
  <120> LEFT    as 'left',
  <121> RIGHT   as 'right',
  <122> SYMBOL  as 'symbol',
  <123> SCOPE   as 'scope',
  <124> ERROR   as 'error',
  <125> WARN    as 'warn',
  <126> HAS     as 'has',
  <127> SET     as 'set',
  <128> UNCASE  as 'uncase',
  <129> PEEK    as 'peek',
  <130> SKIP    as 'skip',
  <131> TOKEN   as 'token',
  <132> NODE    as 'node',
  <133> ALIAS   as 'alias',
  <134> AUTO    as 'auto',
  <135> AS      as 'as',
  <136> IMPORT  as 'import',
  <137> EXPORT  as 'export'
} & T_END;

export token <auto> T_SYMBOLS as map {
  <138> ASTERISK      as '*',
  <139> VERTICAL_BAR  as '|',
  <140> AMPERSAND     as '&',
  <141> PERIOD        as '.',
  <142> COMMA         as ',',
  <143> SEMICOLON     as ';',
  <144> OPEN_BRACES   as '{',
  <145> CLOSE_BRACES  as '}',
  <146> GROUP_OPEN    as '(',
  <147> GROUP_CLOSE   as ')',
  <148> CHEVRON_OPEN  as '<',
  <149> CHEVRON_CLOSE as '>'
};

export token <100> T_IDENTIFIER as (T_ALPHA | T_EXTRA) & opt repeat T_WORD;
export token <101> T_NUMBER     as '0' | from '1' to '9' & opt repeat T_DIGIT;
export token <102> T_STRING     as '\'' & repeat ('\\' then * else not '\'' then *) & '\'';